import groovy.io.GroovyPrintStream
import nl.javadude.gradle.plugins.license.License

import java.nio.file.Paths

/*
 * MIT License
 *
 * Copyright (c) 2017 Choko (choko@curioswitch.org)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id 'java-gradle-plugin'
}

apply plugin: 'maven-publish'

def baselineManifestDir = project.file("$buildDir/gen-baseline")
sourceSets {
    main {
        resources {
            srcDir rootProject.file('.baseline')
        }
        output.dir(baselineManifestDir, builtBy: 'generateBaselineManifest')
    }
}

task generateBaselineManifest {
    doLast {
        project.file("$baselineManifestDir/META-INF/").mkdirs()
        GroovyPrintStream out = new GroovyPrintStream(project.file("$baselineManifestDir/META-INF/org.curioswitch.curiostack.baseline_manifest.txt"))
        def baselineDir = rootProject.file('.baseline')
        baselineDir.eachFileRecurse(groovy.io.FileType.FILES) {file ->
            out.println Paths.get(baselineDir.path).relativize(Paths.get(file.path))
        }
        out.close()
    }
}

dependencies {
    implementation project(':common:google-cloud:cloud-storage')
    implementation project(':tools:gradle-plugins:gradle-conda-plugin')
    implementation project(':tools:gradle-plugins:gradle-golang-plugin')
    implementation project(':tools:gradle-plugins:gradle-protobuf-plugin')
    implementation project(':tools:gradle-plugins:gradle-helpers')
    implementation project(':tools:gradle-plugins:gradle-release-plugin')
    implementation project(':tools:gradle-plugins:gradle-tool-downloader-plugin')

    implementation 'com.bmuschko:gradle-docker-plugin'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-guava'
    implementation 'com.diffplug.spotless:spotless-plugin-gradle'
    implementation 'com.github.ben-manes:gradle-versions-plugin'
    implementation 'com.google.auth:google-auth-library-oauth2-http'
    implementation 'com.google.cloud:google-cloud-kms'
    implementation 'com.google.guava:guava'
    implementation 'com.hubspot.jinjava:jinjava'
    implementation 'com.jfrog.bintray.gradle:gradle-bintray-plugin'
    implementation 'gradle.plugin.com.boxfuse.client:gradle-plugin-publishing'
    implementation 'gradle.plugin.com.google.cloud.tools:jib-gradle-plugin'
    implementation 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties'
    implementation 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin'
    implementation 'io.fabric8:kubernetes-client'
    implementation 'me.champeau.gradle:jmh-gradle-plugin'
    implementation 'net.ltgt.gradle:gradle-apt-plugin'
    implementation 'net.ltgt.gradle:gradle-errorprone-plugin'
    implementation 'nu.studer:gradle-jooq-plugin'
    implementation 'org.bouncycastle:bcpkix-jdk15on'

    // Prevent dependency hell for plugin users by specifying bom'd versions of grpc here
    runtime 'io.grpc:grpc-core'
    runtime 'io.grpc:grpc-netty-shaded'

    // Flyway plugin uses the gradle classpath, so adding this allows flyway to access cloud sql.
    runtime 'com.google.cloud.sql:mysql-socket-factory'
    runtime 'mysql:mysql-connector-java'

    compileOnly project(':common:curio-helpers')

    annotationProcessor 'org.immutables:value'
    compileOnly 'org.immutables:value-annotations'

    testCompile project(':tools:gradle-plugins:gradle-test-helpers')
}

gradlePlugin {
    plugins {
        buildCache {
            id = 'org.curioswitch.gradle-gcloud-build-cache-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.gcloud.GcloudBuildCachePlugin'
        }
        ci {
            id = 'org.curioswitch.gradle-curio-generic-ci-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.ci.CurioGenericCiPlugin'
        }
        cloudbuildGithub {
            id = 'org.curioswitch.gradle-curio-cloudbuild-github-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.cloudbuild.CloudbuildGithubPlugin'
        }
        codelabs {
            id = 'org.curioswitch.gradle-codelabs-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.codelabs.CodelabsPlugin'
        }
        curiostack {
            id = 'org.curioswitch.gradle-curiostack-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.curiostack.CuriostackPlugin'
        }
        database {
            id = 'org.curioswitch.gradle-curio-database-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.gcloud.CurioDatabasePlugin'
        }
        gcloud {
            id = 'org.curioswitch.gradle-gcloud-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.gcloud.GcloudPlugin'
        }
        grpcApi {
            id = 'org.curioswitch.gradle-grpc-api-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.grpcapi.GrpcApiPlugin'
        }
        node {
            id = 'org.curioswitch.gradle-node-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.nodejs.NodePlugin'
        }
        server {
            id = 'org.curioswitch.gradle-curio-server-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.curioserver.CurioServerPlugin'
        }
        curioStaticSite {
            id = 'org.curioswitch.gradle-curio-static-site-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.staticsite.StaticSitePlugin'
        }
        staticSite {
            id = 'org.curioswitch.gradle-static-site-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.staticsite.StaticSitePlugin'
        }
        terraform {
            id = 'org.curioswitch.gradle-terraform-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.terraform.TerraformPlugin'
        }
        web {
            id = 'org.curioswitch.gradle-curio-web-plugin'
            implementationClass = 'org.curioswitch.gradle.plugins.curioweb.CurioWebPlugin'
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            pom {
                name = 'Gradle Curiostack Plugin'
                description = 'A highly opinionated plugin for setting up a gradle codebase ' +
                        'using Curiostack conventions. Defines shared configuration and ' +
                        'applies other useful plugins in an aim to be the only plugin in a ' +
                        'root project.'
                url = 'https://github.com/curioswitch/curiostack/tree/master/' +
                        'tools/gradle-plugins/gradle-curiostack-plugin'
            }
        }
    }
}

// TODO(choko): Figure out how to disable license check of baseline config and reenable.
tasks.withType(License) {
    enabled = false
}

tasks.withType(Test) {
    jvmArgs "-Dorg.curioswitch.curiostack.testing.buildDir=${rootProject.buildDir}"
}

// TODO(choko): Have curiostack plugin do this.
test {
    testLogging {
        testLogging.showStandardStreams = true
        exceptionFormat = 'full'
    }
}
